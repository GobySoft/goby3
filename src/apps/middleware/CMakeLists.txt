if(enable_hdf5)
  add_subdirectory(hdf5)
endif()

add_subdirectory(log_tool)

find_program(LLVM_CONFIG_EXECUTABLE "llvm-config")

if(NOT LLVM_CONFIG_EXECUTABLE)
  message(STATUS "llvm-config NOT found. Will not attempt to build goby_clang_tool. (To install run 'apt install llvm')")
else()  
  message(STATUS "Using ${LLVM_CONFIG_EXECUTABLE} to find LLVM/Clang libraries:")
  execute_process(COMMAND ${LLVM_CONFIG_EXECUTABLE} --cmakedir OUTPUT_VARIABLE LLVM_CMAKE_DIR OUTPUT_STRIP_TRAILING_WHITESPACE)
  message(STATUS "\tUsing LLVM CMake path: ${LLVM_CMAKE_DIR}")
  find_package(LLVM REQUIRED CONFIG PATHS ${LLVM_CMAKE_DIR} NO_DEFAULT_PATH)
  if(LLVM_FOUND)
    message(STATUS "\tFound LLVM version ${LLVM_PACKAGE_VERSION}: ${LLVM_INCLUDE_DIR}")
    find_package(yaml-cpp QUIET)
    if(YAML_CPP_LIBRARIES)
      add_subdirectory(clang_tool)
    else()
      message(STATUS "Could NOT find YAML-CPP. Will not attempt to build goby_clang_tool. (To install run 'apt install libyaml-cpp-dev')")      
    endif()
  else()
    message(STATUS "Could NOT find LLVM. Will not attempt to build goby_clang_tool")
  endif()
endif()
